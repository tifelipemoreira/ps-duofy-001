#include "protheus.ch"
#include "TOTVS.CH"
#include "rwmake.ch"
#include "topconn.ch"
#include "TBICONN.ch"

/*/{Protheus.doc} consUSD
Função realiza a consulta da cotação do dolar na API do BACEN
serÃ¡ feita uma requisição GET para a API do BACEN com a data de consulta com até 5 tentativas de dias anteriores
pois o bacen não retorna a cotação caso não haja cotação para o dia.
@type function
@version  1.0
@author felipe.moreira
@since 06/04/2024
@param dDataConsulta, date, data para consulta da cotação
@return numeric, valor da cotação do dolar
/*/
User Function consUSD(dDataConsulta as date) as numeric
    Local nValor          := 0                                                                                                                                                as numeric
    Local cUrl            := "https://olinda.bcb.gov.br/olinda/servico/PTAX/versao/v1/odata/CotacaoDolarPeriodo(dataInicial=@dataInicial,dataFinalCotacao=@dataFinalCotacao)" as character
    Local cPath           := ""
    Local aHeadOut        := {}                                                                                                                                               as array
    local oRestClient                                                                                                                                                         as object
    local oRet
    local oRetorno
    Local lContinua       := .T.                                                                                                                                              as logical
    Local nQuantidadeDias := 0                                                                                                                                                as numeric

    //Laço para garantir que sejam testados varios dias até encontrar a cotação, pois a API do BACEN não retorna caso o não haja cotação para o dia.
    while lContinua

        //Se passar de 5 dias sem encontrar a cotação, retorna 0 e avisa o usuÃ¡rio que não foi possÃ­vel obter a cotação
        IF nQuantidadeDias > 5
            // devido identificar que a API do BACEN esteve indisponíveis em alguns momentos, foi implementada uma consulta de contingencia em outra API menos conhecida.
            nValor := consultaContingencia(dDataConsulta)
            If nValor == 0
                MSGSTOP( OemToAnsi('Não Foi Possivel Obter a Cotação do Dólar'),"Erro")
                Return nValor := 0
            EndiF
            Return nValor
        EndIF

        //Trata a data para o formato que a API do BACEN aceita
        //busca data de e data até.
        cDataInicial := trataDataParaConsulta(dDataConsulta - nQuantidadeDias)
        cDataFinal   := trataDataParaConsulta(dDataConsulta)
        cPath := "?@dataInicial='"+cDataInicial+"'&@dataFinalCotacao='"+cDataFinal+"'&$format=json"

        oRestClient := FWRest():New(cUrl)
        oRestClient:setPath(cPath)

        //Adiciona o cabeçalho da requisição
        aadd(aHeadOut,'Content-Type: application/json')

        //Define o timeout da requisição 120 segundos evitar
        oRestClient:nTimeOut := 120

        //Faz a requisição GET
        lRet := oRestClient:GET(aHeadOut)
        cRetorno := oRestClient:GetResult()
        oRet := oRestClient:ORESPONSEH

        //Recupera o retorno da API
        oRetorno :=  JsonObject():New()
        ret := oRetorno:FromJson(cRetorno)

        //Se o retorno for um objeto e tiver a propriedade value, pega o valor da cotação
        If ValType(oRetorno) == 'J'
            If ValType(oRetorno["value"]) == 'A' .AND. len(oRetorno["value"]) > 0
                nValor := oRetorno["value"][1]['cotacaoCompra']
                lContinua := .F.
            EndIf
        EndIF

        //se chegou até aqui é porque não encontrou a cotação, então incrementa o contador que serÃ¡ utilizado para diminuir da data de.
        nQuantidadeDias := nQuantidadeDias+1
    EndDo
Return nValor

/*/{Protheus.doc} trataDataParaConsulta
função realiza o tratamento da data padrão do protheus para o formato
aceito na requisição da API do BACEN para consulta da cotação do dolar.
data aceita : MM-DD-YYYY
@type function
@version 1.0
@author felipe.moreira
@since 6/4/2024
@param dDataConsulta, date, data a ser tratada
@return character, data tratada
/*/
Static Function trataDataParaConsulta(dDataConsulta as date) as character
    Local cDia         := Substr(DtoS(dDataConsulta),7,2) as character
    Local cMes         := Substr(DtoS(dDataConsulta),5,2) as character
    Local cAno         := Substr(DtoS(dDataConsulta),1,4) as character
    Local cDataTratada := cMes + "-" + cDia + "-" + cAno  as character

Return cDataTratada

/*/{Protheus.doc} consultaContingencia
função de contingencia para consulta de valor dolar
a api principal que era do BACEN parece não ser 100%
confiavel
@type function
@version 1.0
@author felipe.moreira
@since 11/04/2024
@param dDataConsulta, date, data da consulta
@return numeric, valor do dolar
/*/
Static Function consultaContingencia(dDataConsulta) as numeric
    Local nValor          := 0                                                                                                                                                as numeric
    Local cUrl            := "https://economia.awesomeapi.com.br/" as character
    Local cPath           := "last/USD-BRL"
    Local aHeadOut        := {}                                                                                                                                               as array
    local oRestClient                                                                                                                                                         as object
    local oRet
    local oRetorno


    oRestClient := FWRest():New(cUrl)
    oRestClient:setPath(cPath)

    //Adiciona o cabeçalho da requisição
    aadd(aHeadOut,'Content-Type: application/json')

    //Define o timeout da requisição 120 segundos evitar
    oRestClient:nTimeOut := 120

    //Faz a requisição GET
    lRet := oRestClient:GET(aHeadOut)
    cRetorno := oRestClient:GetResult()
    oRet := oRestClient:ORESPONSEH

    //Recupera o retorno da API
    oRetorno :=  JsonObject():New()
    ret := oRetorno:FromJson(cRetorno)

    //Se o retorno for um objeto e tiver a propriedade value, pega o valor da cotação
    If ValType(oRetorno) == 'J'
        If ValType(oRetorno["USDBRL"]) == 'J' .AND. ValType(oRetorno["USDBRL"]["ask"]) == "C"
            nValor := Round(Val(oRetorno["USDBRL"]["ask"]),2)
        EndIf
    EndIF

Return nValor


/*/{Protheus.doc} testaFuncaoconsUSD
Função para testar a função consUSD que realiza a consulta da cotação do dolar na API do BACEN
@type function
@version  1.0
@author felipe.moreira
@since 4/6/2024
@return numeric, valor da cotação do dolar
/*/
User Function testaFuncaoConsUSD(dDataConsulta as date) as numeric

    RPCSetType(3)
    PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01"

    Default dDataConsulta := dDatabase

    nValor := U_consUSD(dDataConsulta)


    MsgInfo(OemToAnsi('Valor do Dolar para a data passada é: ')+ str(nValor),OemToAnsi("Resultado Teste Função consUSD"))

    RESET ENVIRONMENT
Return nValor

